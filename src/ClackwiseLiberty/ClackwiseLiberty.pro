######################################################################
# .pro file for qmake
######################################################################

TEMPLATE = lib
VERSION = 0.1.0
TARGET = ClackwiseLiberty
DESTDIR = ../../release/lib/
OBJECTS_DIR = build
QMAKE_CPP_MOD_MOC = build/   # prefix for parser source files generated by lex/yacc
INCLUDEPATH += . ../3rdparty/tcl/generic # dir to find tcl.h
LIBS += -L../../release/lib -ltcl8.4

CONFIG += lib_version_first
CONFIG += debug

# Swig
swig_tcl.name = Creating Tcl interface using Swig
swig_tcl.input = SWIG_INTERFACES
swig_tcl.depends = Commands.h
swig_tcl.output = $${OBJECTS_DIR}/${QMAKE_FILE_BASE}_tcl_wrap.cpp
swig_tcl.commands = swig -c++ -tcl8 -o $${OBJECTS_DIR}/${QMAKE_FILE_BASE}_tcl_wrap.cpp ${QMAKE_FILE_IN} 
swig_tcl.variable_out = GENERATED_SOURCES
QMAKE_EXTRA_COMPILERS += swig_tcl

# Input
HEADERS += Liberty/LibGroup.h \
           DotLib.h \
		   LibTrove.h \
		   Troves.h \
		   Commands.h
SOURCES += Liberty/LibGroup.cpp \
           DotLib.cpp \
		   LibTrove.cpp \
		   Troves.cpp \
		   Commands.cpp \
           main.cpp
SWIG_INTERFACES += Commands.i

LEXSOURCES += Liberty/Liberty.l
YACCSOURCES += Liberty/Liberty.y

